BACKUP ~haldamir/backup~
AUTHOR ~berelinde - berelinde@gmail.com~

//MODDER

VERSION ~v3~

README ~haldamir/readme-haldamir.html~

AUTO_TRA ~haldamir/tra/%s~

ALWAYS
  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END
END

LANGUAGE ~English~ ~English~ ~haldamir/tra/english/setup-haldamir.tra~
LANGUAGE ~German~ ~german~ ~haldamir/tra/german/setup-haldamir.tra~

BEGIN @1

/* In case the mod gets several component, this would have to be moved to the ALWAYS block */

ACTION_DEFINE_ARRAY fl#reload BEGIN setup-haldamir END

   LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~haldamir/tra~
      reload_array = fl#reload
  END

APPEND ~STATE.IDS~
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

COPY ~haldamir/audio/blank.mus~ 		~music~
COPY_EXISTING ~songlist.2da~ 		~override~
SET_2DA_ENTRY 0 2 3 ~BLANK.MUS~

/* Haldamir's Soundset */

  LAF HANDLE_AUDIO
    STR_VAR
      audio_path = ~haldamir/audio/english~
      oggdec_path = ~haldamir/audio~
  END


/* ###### old, commented out
ACTION_IF ("%WEIDU_ARCH%" STRING_COMPARE_CASE "x86" = 0) THEN BEGIN // if Windows

  AT_NOW                   ~haldamir/audio-install.bat~   // install...
  AT_INTERACTIVE_UNINSTALL ~haldamir/audio-uninstall.bat~ // ... and uninstall
END ELSE BEGIN
  ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~osx~) THEN BEGIN
  AT_NOW                   ~sh haldamir/audio/audio-install-osx.sh~   // install...
  AT_INTERACTIVE_UNINSTALL ~sh haldamir/audio/audio-uninstall-osx.sh~ // ... and uninstall
  END ELSE BEGIN
    AT_NOW                   ~bash haldamir/audio/audio-install-lin.sh~
    AT_INTERACTIVE_UNINSTALL ~bash haldamir/audio/audio-uninstall-lin.sh~
  END
END

*/


ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
  COPY_EXISTING ~interdia.2da~ ~override~
    SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
    BUT_ONLY_IF_IT_CHANGES
END ELSE BEGIN
  APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
    UNLESS ~BIMOEN2~
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~bimoen2.dlg~ THEN BEGIN
  COMPILE ~haldamir/lib/b!bimoen2.d~
END

INCLUDE ~haldamir/lib/b!ids.tph~
INCLUDE ~haldamir/lib/b!times.tph~

/* creature copying and naming */
COPY ~haldamir/character/b!haldal.bmp~ ~override~
COPY ~haldamir/character/b!haldam.bmp~ ~override~
COPY ~haldamir/character/b!haldas.bmp~ ~override~


/*BGII:EE scales down the portraits, so: */
ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
COPY ~haldamir/character/b!haldal.bmp~ ~override/b!haldam.bmp~
COPY ~haldamir/character/b!haldal.bmp~ ~override/b!haldas.bmp~
END


COPY ~haldamir/character/b!hal.cre~ ~override~
  ADD_CRE_ITEM ~leat04~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
  ADD_CRE_ITEM ~sw1h01~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  ADD_CRE_ITEM ~sw1h02~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  ADD_CRE_ITEM ~potn52~ #0 #0 #0 ~IDENTIFIED~ ~QITEM1~
  ADD_CRE_ITEM ~bow01~ #0 #0 #0 ~IDENTIFIED~ ~INV1~
  ADD_CRE_ITEM ~arow01~ #14 #0 #0 ~IDENTIFIED~ ~QUIVER1~
  ADD_CRE_ITEM ~arow02~ #5 #0 #0 ~IDENTIFIED~ ~QUIVER2~
  SAY NAME1 @1
  SAY NAME2 @1
  WRITE_ASCII 0x248 ~B!HALS~ #8 // assign override script
  WRITE_ASCII 0x2cc ~B!HAL~ #8 // assign pre-joining dialogue file
  WRITE_ASCII 0x280 ~B!HALDAMIR~ #32 // assign DV
  WRITE_ASCII 0x34  ~b!haldas~ #8     // assign small portrait
  WRITE_ASCII 0x3c  ~b!haldam~ #8     // assign medium portrait
  SAY INITIAL_MEETING @2
  SAY MORALE @3
  SAY HAPPY @4
  SAY UNHAPPY_ANNOYED @5
  SAY UNHAPPY_SERIOUS @6
  SAY UNHAPPY_BREAKING @7
  SAY LEADER @8
  SAY TIRED @9
  SAY BORED @10
  SAY BATTLE_CRY1 @11
  SAY BATTLE_CRY2 @12
  SAY BATTLE_CRY3 @13
  SAY BATTLE_CRY4 @14
  SAY DAMAGE @15
  SAY DYING @16
  SAY HURT @17
  SAY AREA_FOREST @18
  SAY AREA_CITY @19
  SAY AREA_DUNGEON @20
  SAY AREA_DAY @21
  SAY AREA_NIGHT @22
  SAY SELECT_COMMON1 @23
  SAY SELECT_COMMON2 @24
  SAY SELECT_COMMON3 @25
  SAY SELECT_COMMON4 @26
  SAY SELECT_COMMON5 @27
  SAY SELECT_COMMON6 @28
  SAY SELECT_ACTION1 @29
  SAY SELECT_ACTION2 @30
  SAY SELECT_ACTION3 @31
  SAY SELECT_ACTION4 @32
  SAY SELECT_ACTION5 @33
  SAY SELECT_ACTION6 @34
  SAY SELECT_ACTION7 @35
  SAY SELECT_RARE1 @36
  SAY SELECT_RARE2 @37
  SAY CRITICAL_HIT @38
  SAY CRITICAL_MISS @39
  SAY TARGET_IMMUNE @40
  SAY INVENTORY_FULL @41
  SAY BIO @42
  
/* dialogue compilation */  
COMPILE ~haldamir/dialogue/b!hal.d~
COMPILE ~haldamir/dialogue/b!halj.d~
COMPILE EVALUATE_BUFFER ~haldamir/dialogue/b!halp.d~
COMPILE ~haldamir/dialogue/bb!hal.d~

/* script compilation */
COMPILE EVALUATE_BUFFER ~haldamir/script/b!hals.baf~
COMPILE ~haldamir/script/b!hald.baf~
EXTEND_TOP ~AR0700.bcs~ ~haldamir/script/b!ar0700.baf~

/* 2da appending */
//2da appending for dialogues
APPEND ~interdia.2da~ ~b!haldamir    bb!hal       bb!hal25~
UNLESS ~b!haldamir~

APPEND ~pdialog.2da~ ~b!haldamir        b!halp             b!halj            b!hald             b!hal25p             b!hal25j             b!hal25d       b!hal25s~
UNLESS ~b!haldamir~
