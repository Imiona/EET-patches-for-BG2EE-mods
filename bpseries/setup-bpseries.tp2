BACKUP ~BPSeries/backup~
AUTHOR ~www.shsforums.net/forum/192-big-picture/~
//Comment out MODDER and change VERSION on official release
VERSION ~v0.3121~
//MODDER setup_tra none area_variables warn missing_extern warn missing_resref warn overwriting_file none
//Change overwriting_file none to overwriting_file warn when it's fixed in a future WeiDU

AUTO_TRA ~bp/language/%s~

LANGUAGE ~English by Horred the Plague~
         ~english~
         ~bpseries/language/english/setup.tra~

LANGUAGE ~German by Leonardo Watson~
         ~german~
         ~bpseries/language/english/setup.tra~
         ~bpseries/language/german/setup.tra~

LANGUAGE ~French by Graoumf and Isaya~
         ~french~
         ~bpseries/language/english/setup.tra~
         ~bpseries/language/french/setup.tra~

LANGUAGE ~Italian by Ilot (?)~
         ~italian~
         ~bpseries/language/english/setup.tra~
         ~bpseries/language/italian/setup.tra~

LANGUAGE ~Russian by prowler~
         ~russian~
         ~bpseries/language/english/setup.tra~
         ~bpseries/language/russian/setup.tra~

LANGUAGE ~Spanish by Lisandro~
         ~spanish~
         ~bpseries/language/english/setup.tra~
         ~bpseries/language/spanish/setup.tra~


BEGIN @5001 DESIGNATED 0

REQUIRE_PREDICATE (GAME_IS ~bg2 tob bgt bgee bg2ee eet~) @7010
///TRA swapping

ACTION_IF GAME_IS ~bgee~ BEGIN
  PRINT @7008
  COPY ~bpseries/language/%LANGUAGE%/utf/Setup.tra~ ~bpseries/language/%LANGUAGE%/Setup.tra~
  LOAD_TRA ~bpseries/language/%LANGUAGE%/Setup.tra~
END ELSE ACTION_IF GAME_IS ~bg2 tob bgt~ BEGIN
  PRINT @7009
  COPY ~bpseries/language/%LANGUAGE%/ansi/Setup.tra~ ~bpseries/language/%LANGUAGE%/Setup.tra~
  LOAD_TRA ~bpseries/language/%LANGUAGE%/Setup.tra~
END


// Detectable Stats v3 by GeN1e/Ardanis
/////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////

OUTER_SPRINT path ~bpseries/ds~ 
INCLUDE ~%path%/ds.tph~
INCLUDE ~bpseries/lib/bp.tph~
INCLUDE ~bpseries/lib/bp_main.tph~

// These will crash DS if left in place
LAF zero_byte_files END


ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
  PRINT ~BG2EE Detected. Just adding spells.~ 
  ACTION_IF NOT FILE_EXISTS ~override/ds_setup.mrk~ AND NOT FILE_EXISTS ~override/dw#setup.mrk~ BEGIN 
    LAF detectable_spells STR_VAR path table = ~cddetect.2da~ END
  END
END ELSE ACTION_IF GAME_IS ~bgee~ BEGIN
  ACTION_IF NOT FILE_EXISTS ~override/ds_setup.mrk~ AND NOT FILE_EXISTS ~override/dw#setup.mrk~ BEGIN 
    LAF detectable_spells_setup STR_VAR path END 
    LAF detectable_spells STR_VAR path table = ~cddetect.2da~ END 
  END ELSE BEGIN
    PRINT @5003
  END
END ELSE BEGIN
  ACTION_IF NOT FILE_EXISTS ~override/ds_setup.mrk~ AND NOT FILE_EXISTS ~override/dw#AIsetup.mrk~ BEGIN  
    LAF detectable_spells_setup STR_VAR path END 
    LAF detectable_spells STR_VAR path table = ~cddetect.2da~ END 
  END
END


ACTION_IF NOT FILE_EXISTS ~override/bpseries.mrk~ BEGIN

  COPY_EXISTING ~sw1h01.itm~ ~override/bpseries.mrk~

  ACTION_IF (GAME_IS ~bgee~) BEGIN
    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~override/dw#setup.mrk~) AND (NOT FILE_EXISTS ~override/bpstats.mrk~) BEGIN
      LAF detectable_weapon_enchantment END
    END ELSE BEGIN
      PRINT @5005
    END
  END ELSE BEGIN
    ACTION_IF NOT FILE_EXISTS ~override/dw#AIsetup.mrk~ BEGIN 
      LAF detectable_weapon_enchantment END
    END ELSE BEGIN
      PRINT @5005
    END
  END

  ACTION_IF NOT FILE_EXISTS ~override/bprules.mrk~ BEGIN
    PRINT @7001
    LAF ids_additions END // BP IDS rule additions (code from BPSeries)
  END
END


ACTION_IF NOT FILE_EXISTS ~override/bpstats.mrk~ BEGIN
  PRINT @7000
  COPY_EXISTING ~sw1h01.itm~ ~override/bpstats.mrk~
  LAF detectable_spells STR_VAR path table = ~bpdetect.2da~ END // BPSeries Detectable Spells Additions
  LAF fix_cloud_detection END


  // Script fix, crashes weidu in the innate fixer stage
  ACTION_IF FILE_EXISTS_IN_GAME ~sarvie01.bcs~ BEGIN
    COPY_EXISTING ~sarvie01.bcs~ ~override~
      DECOMPILE_BCS_TO_BAF
        REPLACE_TEXTUALLY ~Allegiance(Myself,ANYONE)~ ~Allegiance(Myself,EVILCUTOFF)~
      COMPILE_BAF_TO_BCS
    BUT_ONLY
  END
  
  PRINT @7003
  LAF fix_state_diseased END // Fix for BG2 and BGEE STATE_DISEASED (now on stats.ids #134)
  PRINT @7004
  LAF fix_wyvern_mages END // Fix for BG2 and BGEE
END ELSE BEGIN
  PRINT @5006
END


PRINT @7002
LAM reorder_stats_ids  // DS cleanup macro

///////////////////////////////////////////////////////////////////////


BEGIN @7025 DESIGNATED 10

FORBID_COMPONENT "setup-bp.tp2" "10" @5006

INCLUDE ~bpseries/lib/bp.tph~

ACTION_IF GAME_IS ~tob bgt bg2ee eet~ BEGIN
  LAF level_one_innates_bg2 END
END ELSE BEGIN
  LAF level_one_innates END
END


BEGIN @3001 DESIGNATED 100

REQUIRE_FILE ~override/bpseries.mrk~ @5002

INCLUDE ~bpseries/lib/bp.tph~


PRINT @7005
//Make directories, and copies the AICompile.exe if not present 
LAF bp_series_setup END

ACTION_IF GAME_IS ~bgee~ BEGIN
  PRINT @7008
  OUTER_SPRINT scriptpath ~bpseries\source\bgee~
END ELSE ACTION_IF GAME_IS ~bg2ee eet~ BEGIN 
  PRINT @7011
  OUTER_SPRINT scriptpath ~bpseries\source\bg2ee~
END ELSE ACTION_IF GAME_IS ~bg2 bgt tob~ BEGIN
  PRINT @7009
  OUTER_SPRINT scriptpath ~bpseries\source\bg2~
END ELSE BEGIN
  PRINT @7010
END
  
ACTION_IF GAME_IS ~bgee bg2ee eet bg2 bgt tob~ BEGIN

  PRINT @7006
  //Compiles all the BPSeries scripts      
  LAF bp_series_scripts STR_VAR scriptpath END

  PRINT @7007
  //Add entries to scrpdesc.2da for in-game viewing
  LAF scrpdesc_append END
  LAF scrpdesc_replace END
END






