IF
  InParty(Myself)
  Global("EdwinRomanceActive","GLOBAL",2)
  OR(2)
  Global("AnomenRomanceActive","GLOBAL",1)
  Global("AnomenRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("AnomenRomanceActive","GLOBAL",3)
END

IF
  OR(2)
  Global("EdwinRomanceActive","GLOBAL",1)
  Global("EdwinRomanceActive","GLOBAL",2)
  OR(7)
  Global("E3FadeRomanceActive","GLOBAL",2)
  Global("ChloeRomanceActiveCR","GLOBAL",2)
  Global("J#KelseyRomanceActive","GLOBAL",2)
  Global("TsujathaRomanceActive","GLOBAL",2)
  Global("imoenromanceactive","GLOBAL",2)
  Global("H#ValygarRomanceActive","GLOBAL",2)
  Global("SolaTalk","GLOBAL",9)
THEN
  RESPONSE #100
    SetGlobal("EdwinRomanceActive","GLOBAL",3)
END

IF
  Global("EdwinRomanceActive","GLOBAL",2)
  OR(2)
  Global("TsujathaRomanceActive","GLOBAL",1)
  Global("TsujathaRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("TsujathaRomanceActive","GLOBAL",3)
END

IF
  Global("EdwinMatch","GLOBAL",0)
  Global("CheckEdwinMatch","GLOBAL",0)
  Gender(Player1,FEMALE)
  CheckStatGT(Player1,13,INT)
  !Global("AnomenRomanceActive","GLOBAL",2)
  !Global("EdwinRomanceActive","GLOBAL",3)
  OR(5)
  Race(Player1,HUMAN)
  Race(Player1,HALF_ELF)
  Race(Player1,ELF)
  Race(Player1,HALFLING)
  Race(Player1,GNOME)
THEN
  RESPONSE #100
    SetGlobal("CheckEdwinMatch","GLOBAL",1)
    SetGlobal("EdwinMatch","GLOBAL",1)
END

IF
  GlobalTimerExpired("JonDream1","GLOBAL")
  Global("HadJonDream2","GLOBAL",0)
  Global("EddieCantGetDrunk","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("EddieCantGetDrunk","GLOBAL",1)
END

IF
  Global("EddieCantGetDrunk","GLOBAL",1)
  Global("HadJonDream2","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("AfterJonDreams","GLOBAL",ONE_DAY)
    SetGlobal("EddieCantGetDrunk","GLOBAL",2)
END

IF
  GlobalTimerExpired("AfterJonDreams","GLOBAL")
  Global("EddieCantGetDrunk","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("EddieCantGetDrunk","GLOBAL",3)
END

IF
  InParty(Myself)
  Global("EdwinLoveTalk","LOCALS",0)
  Dead("MaeVar")
  Global("EdwinMatch","GLOBAL",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("EdwinRomance","GLOBAL",10)
    SetGlobal("EdwinRomanceActive","GLOBAL",1)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
END

IF
  InParty(Myself)
  PartyRested()
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
  OR(9)
  Global("EdwinLoveTalk","LOCALS",1)
  Global("EdwinLoveTalk","LOCALS",3)
  Global("EdwinLoveTalk","LOCALS",5)
  Global("EdwinLoveTalk","LOCALS",7)
  Global("EdwinLoveTalk","LOCALS",13)
  Global("EdwinLoveTalk","LOCALS",15)
  Global("EdwinLoveTalk","LOCALS",17)
  Global("EdwinLoveTalk","LOCALS",33)
  Global("EdwinLoveTalk","LOCALS",39)
THEN
  RESPONSE #100
    RealSetGlobalTimer("EdwinRomance","GLOBAL",10)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  !AreaCheck("AR1300")
  !AreaType(DUNGEON)
  See(Player1)
  CombatCounter(0)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
  !See([ENEMY])
  !Range([NEUTRAL],10)
  OR(9)
  Global("EdwinLoveTalk","LOCALS",1)
  Global("EdwinLoveTalk","LOCALS",3)
  Global("EdwinLoveTalk","LOCALS",5)
  Global("EdwinLoveTalk","LOCALS",7)
  Global("EdwinLoveTalk","LOCALS",13)
  Global("EdwinLoveTalk","LOCALS",15)
  Global("EdwinLoveTalk","LOCALS",17)
  Global("EdwinLoveTalk","LOCALS",33)
  Global("EdwinLoveTalk","LOCALS",39)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    PlaySong(999990)
    Interact(Player1)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  See(Player1)
  CombatCounter(0)
  OR(2)
  Global("EdwinLoveTalk","LOCALS",11)
  Global("EdwinLoveTalk","LOCALS",37)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    PlaySong(999990)
    Interact(Player1)
END

IF
  InParty(Myself)
  PartyRested()
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  !Gender("Edwin",MALE)
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  See(Player1)
  !AreaType(DUNGEON)
  CombatCounter(0)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
  OR(4)
  Global("EdwinLoveTalk","LOCALS",19)
  Global("EdwinLoveTalk","LOCALS",23)
  Global("EdwinLoveTalk","LOCALS",25)
  Global("EdwinLoveTalk","LOCALS",29)
THEN
  RESPONSE #100
    RealSetGlobalTimer("EdwinRomance","GLOBAL",10)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  !Gender("Edwin",MALE)
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  !AreaType(DUNGEON)
  See(Player1)
  CombatCounter(0)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
  OR(4)
  Global("EdwinLoveTalk","LOCALS",19)
  Global("EdwinLoveTalk","LOCALS",23)
  Global("EdwinLoveTalk","LOCALS",25)
  Global("EdwinLoveTalk","LOCALS",29)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    PlaySong(999991)
    Interact(Player1)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  Global("TalkedDegardan","GLOBAL",1)
  !Gender("Edwin",MALE)
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  See(Player1)
  CombatCounter(0)
  Global("EdwinLoveTalk","LOCALS",27)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    PlaySong(999991)
    Interact(Player1)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  See(Player1)
  CombatCounter(0)
  Global("EdwinDekkieLetter","GLOBAL",2)
  Global("EdwinLoveTalk","LOCALS",41)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    PlaySong(999990)
    Interact(Player1)
END

IF
  InParty(Myself)
  Global("EdwinMatch","GLOBAL",1)
  OR(4)
  Global("EdwinLoveTalk","LOCALS",10)
  Global("EdwinLoveTalk","LOCALS",26)
  Global("EdwinLoveTalk","LOCALS",36)
  Global("EdwinLoveTalk","LOCALS",22)
THEN
  RESPONSE #100
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    RealSetGlobalTimer("EdwinRomance","GLOBAL",1)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  Global("EdwinMatch","GLOBAL",1)
  Global("EdwinLoveTalk","LOCALS",40)
  Global("EdwinTerl","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("SpawnTerlEdwin","GLOBAL",800)
    SetGlobal("EdwinTerl","GLOBAL",1)
END

IF
  InParty(Myself)
  GlobalTimerExpired("SpawnTerlEdwin","GLOBAL")
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
  !AreaCheck("AR1900")
  !AreaCheck("AR2500")
  !AreaCheck("AR2600")
  AreaType(OUTDOOR)
  !Exists("Terl02")
  Global("EdwinRomanceActive","GLOBAL",2)
  Global("EdwinDekkieLetter","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("EdwinDekkieLetter","GLOBAL",1)
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    RealSetGlobalTimer("EdwinRomance","GLOBAL",10)
    CreateCreatureObjectOffScreen("TERL02",Myself,0,0,0)
END

IF
  InParty(Myself)
  RealGlobalTimerExpired("EdwinRomance","GLOBAL")
  Global("EdwinMatch","GLOBAL",1)
  OR(16)
  Global("EdwinLoveTalk","LOCALS",2)
  Global("EdwinLoveTalk","LOCALS",4)
  Global("EdwinLoveTalk","LOCALS",6)
  Global("EdwinLoveTalk","LOCALS",8)
  Global("EdwinLoveTalk","LOCALS",12)
  Global("EdwinLoveTalk","LOCALS",14)
  Global("EdwinLoveTalk","LOCALS",16)
  Global("EdwinLoveTalk","LOCALS",18)
  Global("EdwinLoveTalk","LOCALS",20)
  Global("EdwinLoveTalk","LOCALS",24)
  Global("EdwinLoveTalk","LOCALS",28)
  Global("EdwinLoveTalk","LOCALS",30)
  Global("EdwinLoveTalk","LOCALS",32)
  Global("EdwinLoveTalk","LOCALS",34)
  Global("EdwinLoveTalk","LOCALS",38)
  Global("EdwinLoveTalk","LOCALS",42)
THEN
  RESPONSE #100
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    RealSetGlobalTimer("EdwinRomance","GLOBAL",2800)
END

IF
  InParty(Myself)
  Global("DekkieLetter","GLOBAL",1)
  AreaCheck("AR0300")
  See(Player1)
  CombatCounter(0)
  !Exists("barvon")
  Global("EdwinRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("DekkieLetter","GLOBAL",2)
    CreateCreatureObjectOffScreen("barvon",Myself,0,0,0)
END

IF
  InParty(Myself)
  Global("TalkedToInspector","GLOBAL",1)
  Global("DekkieLetter","GLOBAL",3)
  GlobalTimerExpired("MeetingWithDekaras","GLOBAL")
  AreaCheck("AR0500")
  Range(Player1,45)
  CombatCounter(0)
  Global("EdwinRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("DekkieLetter","GLOBAL",4)
    CreateCreatureObjectOffScreen("dekaras1",Myself,0,0,0)
END

IF
  OR(6)
  AreaCheck("AR0800")
  AreaCheck("AR0801")
  AreaCheck("AR0802")
  AreaCheck("AR0804")
  AreaCheck("AR0805")
  AreaCheck("AR0806")
  Global("EdwinTalkedAboutScroll","GLOBAL",0)
  Global("EdwinMentionsOnce","LOCALS",0)
  InParty(Myself)
  Detect([PC])
  OR(2)
  !Global("EdwinRomanceActive","GLOBAL",1)
  Global("EdwinRomanceNetherScroll","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogNoSet(Player1)
    SetGlobal("EdwinMentionsOnce","LOCALS",1)
END

IF
  Dead("Nevaziah")
  Global("EdwinRomanceActive","GLOBAL",1)
  !Global("EdwinRomanceWarning","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("EdwinRomanceWarning","GLOBAL",3)
END

IF
  RealGlobalTimerExpired("EdwinWantsToBeFeminine","GLOBAL")
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  !AreaCheck("AR0800")
  !AreaCheck("AR0801")
  !AreaCheck("AR0802")
  !AreaCheck("AR0804")
  !AreaCheck("AR0805")
  !AreaCheck("AR0806")
  !AreaCheck("AR0807")
  !AreaCheck("AR0808")
  !AreaCheck("AR0809")
  !AreaCheck("AR0810")
  !AreaCheck("AR0811")
  !AreaCheck("AR0812")
  !AreaCheck("AR0813")
  See(Player1)
  CombatCounter(0)
  Global("EdwinRomanceWarning","GLOBAL",1)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  RealGlobalTimerExpired("EdwinWantsToBeFeminine","GLOBAL")
  !Global("EdwinRomanceActive","GLOBAL",0)
  !Global("EdwinRomanceActive","GLOBAL",3)
  Global("EdwinMatch","GLOBAL",1)
  !AreaCheck("AR0800")
  !AreaCheck("AR0801")
  !AreaCheck("AR0802")
  !AreaCheck("AR0804")
  !AreaCheck("AR0805")
  !AreaCheck("AR0806")
  !AreaCheck("AR0807")
  !AreaCheck("AR0808")
  !AreaCheck("AR0809")
  !AreaCheck("AR0810")
  !AreaCheck("AR0811")
  !AreaCheck("AR0812")
  !AreaCheck("AR0813")
  See(Player1)
  CombatCounter(0)
  Global("EdwinRomanceWarning","GLOBAL",2)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  GlobalTimerExpired("MeetingWithDekaras","GLOBAL")
  Global("DekkieLetter","GLOBAL",1)
  Global("EdwinRomanceActive","GLOBAL",2)
  !AreaCheck("AR0300")
  See(Player1)
  CombatCounter(0)
  !Global("Chapter","GLOBAL",%bg2_chapter_4%)
  !Global("Chapter","GLOBAL",%bg2_chapter_5%)
  !Global("Chapter","GLOBAL",%bg2_chapter_7%)
  OR(2)
  Global("DekarasComplain","GLOBAL",0)
  Global("DekarasComplain","GLOBAL",1)
THEN
  RESPONSE #100
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  Global("CheckEdwinaMatch","GLOBAL",0)
  Gender(Player1,MALE)
THEN
  RESPONSE #100
    SetGlobal("CheckEdwinaMatch","GLOBAL",1)
    SetGlobal("EdwinaFlirt","GLOBAL",1)
END

IF
  GlobalTimerExpired("EdwinFlirt","GLOBAL")
  Global("EdwinaFlirt","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("EdwinaFlirt","GLOBAL",2)
    SetGlobalTimer("EdwinFlirt","GLOBAL",TWO_DAYS)
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  GlobalTimerExpired("EdwinFlirt","GLOBAL")
  Global("EdwinaFlirt","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("EdwinaFlirt","GLOBAL",4)
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  AreaCheck("AR0527")
  Global("EdwinRomanceActive","GLOBAL",2)
  Global("EdwinLichQuest","GLOBAL",1)
  Global("Dekaras2Spawn","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Dekaras2Spawn","LOCALS",1)
    CreateCreature("dekaras2",[506.138],0)
    SmallWait(1)
    ActionOverride("dekaras2",MoveToPointNoInterrupt([537.365]))
    SmallWait(1)
    ActionOverride("dekaras2",FaceObject(Player1))
    ActionOverride("dekaras2",Dialog(Player1))
END

IF
  GlobalTimerExpired("Eddiedekkiedelaymad","GLOBAL")
  !Global("EdwinLichQuest","GLOBAL",2)
  InParty(Myself)
THEN
  RESPONSE #100
    Dialog(Player1)
END

IF
  AreaCheck("AR0500")
  Global("EdCreatedInvis","LOCALS",0)
  Global("EdwinRomanceActive","GLOBAL",2)
  Global("EdwinLichQuest","GLOBAL",1)
THEN
  RESPONSE #100
    CreateCreature("Eddekfhi",[2949.822],0)
    SetGlobal("EdCreatedInvis","LOCALS",1)
END

IF
  Global("Edwinmentionedthesmell","LOCALS",0)
  Global("EdwinRomanceActive","GLOBAL",2)
  Global("EdwinLichQuest","GLOBAL",1)
  Range("erfish1",30)
THEN
  RESPONSE #100
    StartDialogNoSet(Player1)
    ActionOverride("erfish1",DestroySelf())
END

IF
  Global("TalkedDegardan","GLOBAL",1)
  !Global("EdwinaFlirt","GLOBAL",0)
  Global("Edwinahaddegdia","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Edwinahaddegdia","LOCALS",1)
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  Global("TalkedDegardan","GLOBAL",4)
  !Global("EdwinaFlirt","GLOBAL",0)
  Global("Edwinahaddegdia","LOCALS",2)
  Gender("Edwin",MALE)
THEN
  RESPONSE #100
    SetGlobal("Edwinahaddegdia","LOCALS",3)
    MoveViewObject("Edwin",INSTANT)
    StartDialogNoSet(Player1)
END

IF
  AttackedBy([PC],DEFAULT)
  AreaCheck("arbase")
  Global("Edwattpc","GLOBAL",0)
  Global("Dekattpc","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("Edwattpc","GLOBAL",1)
    ActionOverride("dekaras3",StartDialogNoSet(Player1))
END

IF
  !InParty(Myself)
  Global("EdwinVampire","GLOBAL",4)
THEN
  RESPONSE #100
    SetGlobal("EdwinVampire","GLOBAL",5)
    StartDialogNoSet(Player1)
END

IF
  PartyRested()
  Global("BancoDead","GLOBAL",1)
  Global("DekkieBye","GLOBAL",1)
  Global("EdwinLoveTalk","LOCALS",43)
THEN
  RESPONSE #100
    IncrementGlobal("EdwinLoveTalk","LOCALS",1)
    IncrementGlobal("EdwinLoveTalkCheck","GLOBAL",1)
    Interact(Player1)
END

