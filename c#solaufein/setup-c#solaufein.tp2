/* 

*/

BACKUP ~C#Solaufein\Install\backup~
AUTHOR ~jastey@web.de~ 


VERSION ~Alpha_160128~

README ~C#Solaufein/readme.%LANGUAGE%.txt~ ~C#Solaufein/readme.german.txt~

AUTO_TRA ~C#Solaufein\Translations\autotra/%s~


ALWAYS
  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END



    OUTER_SET SOLAUFEIN_TIMER = 1200
    OUTER_SET SOLAUFEIN_TIMERTOB = 900

ACTION_IF GAME_IS ~bg2 tob bgt~ BEGIN
    OUTER_SPRINT ~Drideranimation_male~ ~DRIDER_MALE~
    OUTER_SPRINT ~Drideranimation_female~ ~DRIDER_FEMALE~
END

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
    OUTER_SPRINT ~Drideranimation_male~ ~IC2_DRIDER_MALE~
    OUTER_SPRINT ~Drideranimation_female~ ~IC2_DRIDER_FEMALE~
END


/* all following actions are only processed ONCE for the whole mod, independent of un- and reinstalling of single components */

ACTION_IF !FILE_EXISTS ~C#Solaufein\Install/c#solaufeinreinstall.mrk~ BEGIN

  /*
   * Copies tra files into the autotra-folder (to leave the originals untouched)
   */
  DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
    COPY ~C#Solaufein\Translations/%LANGUAGE%~  ~C#Solaufein\Translations/autotra/%LANGUAGE%~
  END

  LAF autotra_workaround END


ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END

ACTION_DEFINE_ARRAY fl#reload BEGIN game END

   LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~C#Solaufein\Translations/autotra~
      noconvert_array = fl#noconvert
      reload_array = fl#reload
  END




COPY ~C#Solaufein/install/component.xx~ ~C#Solaufein\Install/c#solaufeinreinstall.mrk~

END //c#solaufeinreinstall.mrk


    //TRAs declared in LANGUAGE must be reloaded
  LOAD_TRA ~C#Solaufein\Translations/autotra/%LANGUAGE%/game.tra~


END //ALWAYS


LANGUAGE ~Deutsch~
         ~german~   
         ~C#Solaufein\Translations\autotra/german\setup.tra~
         ~C#Solaufein\Translations\autotra/german\game.tra~

LANGUAGE ~English (still great parts in German)~
         ~english~   
         ~C#Solaufein\Translations\autotra/english\setup.tra~
         ~C#Solaufein\Translations\autotra/english\game.tra~


BEGIN @90000

//CamDawgs CD_STATE_NOTVALID zum Checken der Dialogfähigkeit eines Char
//Thank you very much, CamDawg!
APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
 ~0x80101FEF CD_STATE_NOTVALID~
 UNLESS ~CD_STATE_NOTVALID~

  /* Tells other mods Solaufein core is installed */
  COPY ~C#Solaufein/Install/component.xx~ ~override/C#SolaufeinCore.RR~


ADD_JOURNAL TITLE (@1799) @67 @1795 @1796 @1797 @1798 USING ~C#Solaufein\Translations\autotra/%LANGUAGE%/C#SOLATRA.TRA~




COPY_EXISTING ~ar2100.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 18
    fj_box_top      = 2509
    fj_box_right    = 103
    fj_box_bottom   = 2689
    fj_cursor_idx   = 30   //door
    fj_vertex_0     = 103 + (2509 << 16)
    fj_vertex_1     = 90 + (2509 << 16)
    fj_vertex_2     = 90 + (2689 << 16)
    fj_vertex_3     = 203 + (2689 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = Tranc#so
    fj_destination_area = c#so01
    fj_destination_name = Exit2100
  END


  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 147
    fj_loc_y       = 2634
    fj_orientation = 12   //E
    STR_VAR
    fj_structure_type = entrance
    fj_name           = Exitc#so
  END



//trigger point with script
  LPF ~fj_are_structure~
    INT_VAR
    fj_type         = 1    //trigger
    fj_box_left     = 18
    fj_box_top      = 2509
    fj_box_right    = 103
    fj_box_bottom   = 2689
    fj_cursor_idx   = 30   //door
    fj_vertex_0     = 103 + (2509 << 16)
    fj_vertex_1     = 90 + (2509 << 16)
    fj_vertex_2     = 90 + (2689 << 16)
    fj_vertex_3     = 203 + (2689 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#sorune
    fj_reg_script   = c#sopass
END




//container and item
  LPF ~fj_are_structure~
    INT_VAR
    fj_type        = 8 //nonvisible
    fj_loc_x       = 3810
    fj_loc_y       = 1962
    fj_box_left    = 3809
    fj_box_top     = 1783
    fj_box_right   = 3824
    fj_box_bottom  = 1826
    fj_trap_loc_x  = 3810
    fj_trap_loc_y  = 1962
    fj_vertex_0    = 3809 + (1880 << 16)
    fj_vertex_1    = 3818 + (1823 << 16)
    fj_vertex_2    = 3824 + (1826 << 16)
    fj_vertex_3    = 3819 + (1783 << 16)
    STR_VAR
    fj_structure_type = container
    fj_name           = ~Felsspalte~
  END

BUT_ONLY




/* areas */
COPY ~C#Solaufein\area~ ~Override~

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
COPY ~C#Solaufein\area\C#SO01_EE.MOS~ ~Override\C#SO01.MOS~
END


/* bams */
COPY ~C#Solaufein\bams~ ~Override~


/* vvc */
COPY ~C#Solaufein\vvc~ ~Override~



/* effects */
COPY ~C#Solaufein\eff~ ~Override~


/* items */

COPY ~C#Solaufein/items/c#sorun1.itm~ ~override/c#sorun1.itm~
SAY NAME1 @1
SAY NAME2 @1
SAY UNIDENTIFIED_DESC @1
SAY DESC  @2

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
COPY_EXISTING  ~c#sorun1.itm~ ~override~
WRITE_ASCII 0x58 ~CMISC2N~ #8
END


COMPILE ~C#Solaufein\Dialogues\C#SOLAU.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE EVALUATE_BUFFER ~C#Solaufein\Dialogues\C#SOLAUB.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
COMPILE EVALUATE_BUFFER ~C#Solaufein\Dialogues\C#SOLAUB_ee.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
END

COMPILE EVALUATE_BUFFER ~C#Solaufein\Dialogues\C#SOLAUJ.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Dialogues\c#solaufein_interjections.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Dialogues\scenery_dialogues_soa.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 


/* ------------  tob ------------------ - */


ACTION_IF GAME_IS ~tob bg2ee eet~ THEN BEGIN 

  COMPILE ~C#Solaufein\Dialogues\C#SOLAU_ToB.d~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 

  //  epilogue: ##
  EXTEND_TOP ~AR6200.bcs~ ~c#solaufein\scripts\solau_epilog_AR6200.baf~

  /* no friendship */
  COPY ~c#solaufein\2da\c#soepxx.2da~ ~override/c#soep01.2da~
  REPLACE ~99999~ @3

  /* friendship */
  COPY ~c#solaufein\2da\c#soepxx.2da~ ~override/c#soep02.2da~
  REPLACE ~99999~ @4

END

/* -------------------------------------- */


//The PID, to be compiled as last dialogue file
 COMPILE EVALUATE_BUFFER ~C#Solaufein\Dialogues\C#SOLAU_PID.d~ 
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 


COMPILE EVALUATE_BUFFER ~C#Solaufein\Scripts\C#SOLAU.baf~


COMPILE ~C#Solaufein\Scripts\C#SOLAU1.baf~
COMPILE ~C#Solaufein\Scripts\c#socut1.baf~
COMPILE ~C#Solaufein\Scripts\c#socut2.baf~


COMPILE ~C#Solaufein\Scripts\c#sodrsc.baf~
COMPILE ~C#Solaufein\Scripts\c#sodrf0.baf~
COMPILE ~C#Solaufein\Scripts\c#sodrm1.baf~

COMPILE ~C#Solaufein\Scripts\c#so01.baf~

COMPILE ~C#Solaufein\Scripts\c#sopass.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sococ1.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sococ2.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sococ3.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sococ4.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sococ5.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sococ6.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 
COMPILE ~C#Solaufein\Scripts\c#sobrut.baf~
USING ~C#Solaufein\Translations\autotra/%s\c#solatra.tra~ 


EXTEND_TOP ~AR2100.bcs~ ~C#Solaufein\Scripts\C#Solau_AR2100.baf~

EXTEND_TOP ~AR2401.bcs~ ~C#Solaufein\Scripts\C#Solau_AR2401.baf~

EXTEND_TOP ~AR2500.bcs~ ~C#Solaufein\Scripts\C#Solau_AR2500.baf~





/* Solaufein kit */

INCLUDE ~C#Solaufein\lib/fl#add_kit_ee.tpa~

ADD_KIT ~C#SoDrow~ ///Solaufein's drow kit
  // appended to CLASWEAP.2da
  ~C#SoDrow                 1           1           1           1           1           1           1           1~

  // appended column-wise to WEAPPROF.2da
  ~C#SoDrow  5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 2 2 2 3 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5~
     
  // appended to ABCLASRQ.2da
  ~C#SoDrow                 9       0       0       0       0       0~

  // appended to ABCLSMOD.2da
  ~C#SoDrow                 0       0       0       0       0       0~

  // appended to ABDCDSRQ.2da
  ~C#SoDrow                 17      0       0       0       0       0~

  // appended to ABDCSCRQ.2da
  ~C#SoDrow                 15      0       0       0       0       0~

  // appended to ALIGNMNT.2da
  ~C#SoDrow                 1       1       1       1       1       1       1       1       1~

  // appended to DUALCLAS.2da
  ~C#SoDrow                 0       0       0       0       0       0~

  // path to your CLAB-style 2da file 
  ~C#Solaufein/2da/C#SoDrow.2da~

  // PC races and classes allowed to use this kit, from KITTABLE.2da
  ~~

  // usability flags, added to the end of the KITLIST.2da entry
  ~0x00004000    2~       ///vanilla fighter

  // HLA table to use; see LUABBR.2da
  ~Fi0~

  // list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
  ~* * * * * * * * * * * * * * * * * * * *~

  // Lower, mixed, and help kit names and descriptions, added to KITLIST.2da 
  SAY @5
  SAY @5
  SAY @6


LAF fl#add_kit_ee STR_VAR kit_name = C#SoDrow END


/*  spells */


COPY ~C#Solaufein\spells/c#soblin.spl~ ~override/c#soblin.spl~
COPY ~C#Solaufein\spells/c#sobrec.spl~ ~override/c#sobrec.spl~
COPY ~C#Solaufein\spells/c#somar2.spl~ ~override/c#somar2.spl~


COPY ~C#Solaufein\spells/c#sofeen.spl~ ~override/c#sofeen.spl~
  SAY NAME1 @7 

COPY ~C#Solaufein\spells/c#sodark.spl~ ~override/c#sodark.spl~
  SAY NAME1 @8

COPY ~C#Solaufein\spells/c#sodimd.spl~ ~override/c#sodimd.spl~
  SAY NAME1 @9


/* Portraits */

ACTION_IF GAME_IS ~bg2 tob bgt~ BEGIN

COPY ~C#Solaufein/Portraits/C#SolauL.bmp~ ~override/C#SolauL.bmp~
COPY ~C#Solaufein/Portraits/C#SolauM.bmp~ ~override/C#SolauM.bmp~
COPY ~C#Solaufein/Portraits/C#SolauS.bmp~ ~override/C#SolauS.bmp~

END

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN

COPY ~C#Solaufein/Portraits/C#SolauL.bmp~ ~override/C#SolauL.bmp~
COPY ~C#Solaufein/Portraits/Solaufein_EE/C#SolauM.bmp~ ~override/C#SolauM.bmp~
COPY ~C#Solaufein/Portraits/Solaufein_EE/C#SolauS.bmp~ ~override/C#SolauS.bmp~

END




/* cre */


COPY ~C#Solaufein\Creatures\C#SOlau1.cre~ ~override\C#SOlau1.CRE~ 

/*

ACTION_IF (FILE_EXISTS ~C#Solaufein/sound/%LANGUAGE%~) BEGIN
  LAF HANDLE_AUDIO
    STR_VAR
      audio_path = EVAL ~C#Solaufein/sound/%LANGUAGE%~
      oggdec_path = ~C#Solaufein/sound~
  END
END
ELSE BEGIN
  LAF HANDLE_AUDIO
    STR_VAR
      audio_path = ~C#Solaufein/sound/english~
      oggdec_path = ~C#Solaufein/sound~
  END
END

*/


ACTION_IF (FILE_EXISTS ~C#Solaufein/sound/%LANGUAGE%~) BEGIN
  COPY ~C#Solaufein/sound/%LANGUAGE%~ ~override~
END
ELSE BEGIN
  COPY ~C#Solaufein/sound/english~ ~override~
END

COPY 	~C#Solaufein\Creatures\C#Sola12.cre~ ~override\C#Sola12.cre~
 	~C#Solaufein\Creatures\C#Sola14.cre~ ~override\C#Sola14.cre~
 	~C#Solaufein\Creatures\C#Sola16.cre~ ~override\C#Sola16.cre~
 	~C#Solaufein\Creatures\C#Soltob.cre~ ~override\C#Soltob.cre~
  WRITE_SHORT   0x244 0
  WRITE_SHORT 0x246 0x4000 + ~%C#SoDrow%~
SAY NAME1 #50874 //~Solaufein~
SAY NAME2 #50874 //~Solaufein~
  SAY INITIAL_MEETING @12
  SAY MORALE @13
  SAY HAPPY @14
  SAY UNHAPPY_ANNOYED @15
  SAY UNHAPPY_SERIOUS @16
  SAY UNHAPPY_BREAKING @16
  SAY LEADER @17
  SAY TIRED @18
  SAY BORED @19 
  SAY BATTLE_CRY1 @20
  SAY BATTLE_CRY2 @21
  SAY BATTLE_CRY3 @20
  SAY BATTLE_CRY4 @22
  SAY BATTLE_CRY5 @21
  SAY DAMAGE ~~ [UDSOLA05]
  SAY DYING ~~ [UDSOLA06]
  SAY HURT @22
  SAY AREA_FOREST ~~ []
  SAY AREA_CITY ~~ []
  SAY AREA_DUNGEON @30
  SAY AREA_DAY ~~ []
  SAY AREA_NIGHT ~~ []
  SAY SELECT_COMMON1 @24
  SAY SELECT_COMMON2 @17
  SAY SELECT_COMMON3 @15
  SAY SELECT_COMMON4 @24
  SAY SELECT_COMMON5 @26
  SAY SELECT_COMMON6 @25
  SAY SELECT_ACTION1 @27
  SAY SELECT_ACTION2 @17
  SAY SELECT_ACTION3 @26
  SAY SELECT_ACTION4 @23
  SAY SELECT_ACTION5 @24
  SAY SELECT_ACTION6 @12
  SAY SELECT_ACTION7 @25
//  SAY SELECT_RARE1 ~~ []
//  SAY SELECT_RARE2 ~~ []
  SAY CRITICAL_HIT @28
  SAY CRITICAL_MISS @29
  SAY TARGET_IMMUNE @29
  SAY INVENTORY_FULL ~~ []
  SAY PICKED_POCKET ~~
  SAY HIDDEN_IN_SHADOWS ~~
  SAY SPELL_DISRUPTED @29
  SAY SET_A_TRAP ~~
SAY BIO @10


COPY ~C#Solaufein\Creatures\C#SOcre1.cre~ ~override\C#SOcre1.CRE~  //Imrae
COPY ~C#Solaufein\Creatures\c#sodrf0.cre~ ~override\c#sodrf0.CRE~ 
WRITE_ASCII 0x248 ~c#sodrsc~ #8  //override script
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrf0~ #8
WRITE_ASCII 0x2cc ~c#sodrf0~ #8 
COPY ~C#Solaufein\Creatures\c#sodrf0.cre~ ~override\c#sodrf1.CRE~
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrf1~ #8
WRITE_ASCII 0x2cc ~c#sodrf1~ #8 
COPY ~C#Solaufein\Creatures\c#sodrm0.cre~ ~override\c#sodrm0.CRE~ 
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrm0~ #8
WRITE_ASCII 0x2cc ~c#sodrm0~ #8 
COPY ~C#Solaufein\Creatures\c#sodrm0.cre~ ~override\c#sodrm1.CRE~
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrm1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrm1~ #8
WRITE_ASCII 0x2cc ~c#sodrm1~ #8 
COPY ~C#Solaufein\Creatures\c#sodrc0.cre~ ~override\c#sodrc0.CRE~ 
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~C#solau1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrc0~ #8
WRITE_ASCII 0x2cc ~c#sodrc0~ #8 

/* Driders. Polymorph to drider-animation via script in-game! */

/* male */
COPY ~C#Solaufein\Creatures\c#sodrf0.cre~ ~override\c#sodrd1.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd1~ #8
WRITE_ASCII 0x2cc ~c#sodrd1~ #8 

COPY ~C#Solaufein\Creatures\c#sodrf0.cre~ ~override\c#sodrd2.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrm1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd2~ #8
WRITE_ASCII 0x2cc ~c#sodrd2~ #8 

COPY ~C#Solaufein\Creatures\c#sodrm0.cre~ ~override\c#sodrd3.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd3~ #8
WRITE_ASCII 0x2cc ~c#sodrd3~ #8 

/* female */
COPY ~C#Solaufein\Creatures\c#sodrc0.cre~ ~override\c#sodrd4.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd4~ #8
WRITE_ASCII 0x2cc ~c#sodrd4~ #8 

COPY ~C#Solaufein\Creatures\c#sodrf0.cre~ ~override\c#sodrd5.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrm1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd5~ #8
WRITE_ASCII 0x2cc ~c#sodrd5~ #8 

/* spiders */

COPY ~C#Solaufein\Creatures\c#sospd1.cre~ ~override\c#sospd1.CRE~
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x280 ~c#sospd1~ #8
WRITE_ASCII 0x2cc ~c#sospd1~ #8 


/* dead spiders */

COPY ~C#Solaufein\Creatures\c#sospdd.cre~ ~override\c#sospdd.CRE~



//Dialoguesets

//Dialoguesets für installiertes ToB
APPEND ~pdialog.2da~
~C#Solaufein 	C#SolauP 	C#SolauJ 	C#SolauD 	C#SolauP 	C#SolauJ 		C#SolauD 	C#Solau~
UNLESS ~C#Solaufein~


APPEND ~interdia.2da~
~C#Solaufein 	C#SolauB      C#SolauB~
UNLESS ~C#Solaufein~

//Damit Imoen auch was sagen darf [taken from BGII fixpack at G3]
ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
  COPY_EXISTING ~interdia.2da~ ~override~
    SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
    BUT_ONLY_IF_IT_CHANGES
END ELSE BEGIN
  APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
    UNLESS ~BIMOEN2~
END


/* Drider animations: BGII:EE (female/male) 
taken out of viader's Extended Animations Mod with many thanks! */

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN

APPEND ~ANIMATE.IDS~
~0xe282 IC2_DRIDER_FEMALE
0xe283 IC2_DRIDER_MALE~

APPEND ~EXTANIM.2DA~
~57986 0 1 1 0 0 0 47 0 0 NONE NONE NONE 8 10 MDRF NONE 0 0 NONE
57987 0 1 1 0 0 0 47 0 0 NONE NONE NONE 8 10 MDRM NONE 0 0 NONE~


APPEND ~EXTSPEED.2DA~
~57986	9
57987	9~

APPEND ~anisnd.ids~ 
~0xe282 MDRF CGAMEANIMATIONTYPE_IC2_DRIDER_FEMALE
0xe283  MDRM CGAMEANIMATIONTYPE_IC2_DRIDER_MALE~


COPY ~C#Solaufein/animations/ee/2da~ ~override~
COPY ~C#Solaufein/animations/ee/sound~ ~override~
COPY ~C#Solaufein/animations/ee/bam~ ~override~

EXTEND_BOTTOM ~C#SO01.bcs~ ~c#solaufein\scripts\C#So01_driderchange.baf~ EVALUATE_BUFFER

COPY_EXISTING ~C#sococ2.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1251\.844\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ3.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd5",\[1456\.1083\],[0S])\)~
~\1 ActionOverride("c#sodrd5",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd5",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ5.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1592\.1241\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES





END



ACTION_IF GAME_IS ~eet~ BEGIN
  INCLUDE ~EET/other/EET_functions.tph~
  LAF ~EET_NPC_TRANSITION~
    INT_VAR
      type = 2
    STR_VAR
      dv = "C#Solaufein"
      override_SoA = "C#SOLAU"
      override_ToB = "C#SOLAU"
      dialog_ToB = "C#SOLA25"
      traFile = EVAL "C#Solaufein/Translations/autotra/%LANGUAGE%/c#solatra.tra"
      string = "@1328" //Bring me Solaufein, the drow fighter, who I saved from the drider cocoons.
      stringPosDV = "Tiax"
  END
END ELSE BEGIN
  COMPILE ~c#solaufein/Dialogues/FATESP.d~ USING ~C#Solaufein/Translations/autotra/%LANGUAGE%/c#solatra.tra~
END


/*
/////////////////////////////////////
 Give Ust Natha Solaufein the Portrait 
/////////////////////////////////////
*/

BEGIN @90001

REQUIRE_FILE ~override/C#SolaufeinCore.RR~ ~Jastey's Solaufein mod is not installed~

/* Give Solaufein in Ust Natha the portrait! */

COPY_EXISTING ~UDSOLA01.CRE~ ~override~
	~UDSOLA02.CRE~ ~override~
WRITE_ASCII 0x34 ~C#SOLAUS~
WRITE_ASCII 0x3c ~C#SOLAUM~
BUT_ONLY_IF_IT_CHANGES






/*
/////////////////////////////////////
 Drider animations 
/////////////////////////////////////

 */

BEGIN @90002

REQUIRE_PREDICATE !(GAME_IS ~bg2ee eet~) ~This component is not required for BGII:EE~
REQUIRE_PREDICATE ((GAME_IS ~bg2 tob bgt~) AND (MOD_IS_INSTALLED ~SETUP-INFINITYANIMATIONS.TP2~ "0") AND (FILE_EXISTS ~override/µaba1.bam~) AND (FILE_EXISTS_IN_GAME ~override/µaca1.bam~)) ~The required core component of the animation mod is not installed or the animation is missing, refer to readme.~ 

REQUIRE_FILE ~override/C#SolaufeinCore.RR~ ~Jastey's Solaufein mod is not installed~


/* BGII / BGT version: IA needs to be installed for the drider animations */


EXTEND_BOTTOM ~C#SO01.bcs~ ~c#solaufein\scripts\C#So01_driderchange.baf~ EVALUATE_BUFFER

COPY_EXISTING ~C#sococ2.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1251\.844\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ3.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd5",\[1456\.1083\],[0S])\)~
~\1 ActionOverride("c#sodrd5",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd5",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ5.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1592\.1241\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

