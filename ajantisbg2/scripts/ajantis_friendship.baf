/* Ajantis non-romance dialogue. When timer "C#AjantisFriendTalkT" goes off   
Ajantis starts dialogue with player1. */


/* Inside a tavern. Triggers after FT 6. Dialogue activation */
IF
  InParty(Myself)
  !AreaType(DUNGEON)
  InMyArea(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !See([ENEMY])
  !Range([NEUTRAL],10)
  CombatCounter(0)
    !Global("C#AjantisEngaged","GLOBAL",1)
    !Global("C#AjantisRomanceActive","GLOBAL",2)
	!Global("Chapter","GLOBAL",%bg2_chapter_4%)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)
	!Global("Chapter","GLOBAL",%bg2_chapter_7%)
OR(10)
AreaCheck("AR0704")
AreaCheck("AR0709")
AreaCheck("AR0406")
AreaCheck("AR0513")
AreaCheck("AR0509")
AreaCheck("AR0021")
AreaCheck("AR0313")
AreaCheck("AR1105")
AreaCheck("AR2010")
AreaCheck("AR1602")
GlobalGT("C#AjantisFriendTalk","GLOBAL",12)
Global("C#AjantisSchweigen","GLOBAL",0)
Global("C#AjantisFTTavernDrink","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("C#AjantisFTTavernDrink","GLOBAL",1)
END 

/* Inside a tavern. Triggers after FT 6. Dialogue triggering */

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjantisFTTavernDrink","GLOBAL",1)
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#AjantisFriendTalkT","GLOBAL",%C#AJROM_TIMER%)
		PlaySong(0)
		PlaySound("C#ajanFT")
		StartDialogueNoSet(Player1)
END


//Global("C#AjantisSchweigen","GLOBAL",1): PC doesn't want friendship talks 

IF
  InParty(Myself)
  RealGlobalTimerExpired("C#AjantisFriendTalkT","GLOBAL")
  PartyRested()
  See(Player1)
  !AreaType(DUNGEON)
  CombatCounter(0)
    !Global("C#AjantisEngaged","GLOBAL",1)
    !Global("C#AjantisRomanceActive","GLOBAL",2)
OR(10)
    Global("C#AjantisFriendTalk","GLOBAL",1)
    Global("C#AjantisFriendTalk","GLOBAL",3)
    Global("C#AjantisFriendTalk","GLOBAL",5)
    Global("C#AjantisFriendTalk","GLOBAL",7)
    Global("C#AjantisFriendTalk","GLOBAL",9)
    Global("C#AjantisFriendTalk","GLOBAL",11)
    Global("C#AjantisFriendTalk","GLOBAL",13)
    Global("C#AjantisFriendTalk","GLOBAL",15)
    Global("C#AjantisFriendTalk","GLOBAL",17)
    Global("C#AjantisFriendTalk","GLOBAL",19)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#AjantisFriendTalkT","GLOBAL",10)
END

/* Open up globals - 1st cycle. Before going to spellhold / underdark */

IF
  InParty(Myself)
  RealGlobalTimerExpired("C#AjantisFriendTalkT","GLOBAL")
  !AreaType(DUNGEON)
  !AreaCheck("AR1300")
  InMyArea(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !See([ENEMY])
  !Range([NEUTRAL],10)
  CombatCounter(0)
  Global("C#AjantisSchweigen","GLOBAL",0)  
    !Global("C#AjantisEngaged","GLOBAL",1)
    !Global("C#AjantisRomanceActive","GLOBAL",2)
OR(7)
    Global("C#AjantisFriendTalk","GLOBAL",1)
    Global("C#AjantisFriendTalk","GLOBAL",3)
    Global("C#AjantisFriendTalk","GLOBAL",5)
    Global("C#AjantisFriendTalk","GLOBAL",7)
    Global("C#AjantisFriendTalk","GLOBAL",9)
    Global("C#AjantisFriendTalk","GLOBAL",11)
    Global("C#AjantisFriendTalk","GLOBAL",13)
THEN
  RESPONSE #100
    IncrementGlobal("C#AjantisFriendTalk","GLOBAL",1)
END

/* triggering  - 1st cycle. Before going to spellhold / underdark */

IF
  InParty(Myself)
  RealGlobalTimerExpired("C#AjantisFriendTalkT","GLOBAL")
  InMyArea(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !Range([NEUTRAL],10)
  Global("C#AjantisSchweigen","GLOBAL",0)
  !Global("C#AjantisEngaged","GLOBAL",1)
  !Global("C#AjantisRomanceActive","GLOBAL",2)
  OR(7)
    Global("C#AjantisFriendTalk","GLOBAL",2)
    Global("C#AjantisFriendTalk","GLOBAL",4)
    Global("C#AjantisFriendTalk","GLOBAL",6)
    Global("C#AjantisFriendTalk","GLOBAL",8)
    Global("C#AjantisFriendTalk","GLOBAL",10)
    Global("C#AjantisFriendTalk","GLOBAL",12) 
    Global("C#AjantisFriendTalk","GLOBAL",14) 
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#AjantisFriendTalkT","GLOBAL",%C#AJROM_TIMER%)
		PlaySong(0)
		PlaySound("C#ajanFT")
 StartDialogueNoSet(Player1)
END

/* "encounter"  - 1st cycle. Before going to spellhold / underdark */
IF
  InParty(Myself)
  RealGlobalTimerExpired("C#AjantisFriendTalkT","GLOBAL")
  !AreaType(DUNGEON)
  !AreaCheck("AR1300")
  !AreaCheck("AR2800")
	!Global("Chapter","GLOBAL",%bg2_chapter_4%)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)
	!Global("Chapter","GLOBAL",%bg2_chapter_7%)
AreaType(CITY)
  InMyArea(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !See([ENEMY])
  !Range([NEUTRAL],10)
  CombatCounter(0)
  Global("C#AjantisSchweigen","GLOBAL",0)  
    !Global("C#AjantisEngaged","GLOBAL",1)
    !Global("C#AjantisRomanceActive","GLOBAL",2)
 Global("C#AjantisFriendTalk","GLOBAL",15)
Global("C#AjantisFTTavernDrink","GLOBAL",2)
Global("C#AjantisFTVerwechslung","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("C#AjantisFTVerwechslung","GLOBAL",1)
END

/* encounter: creation of the prostitute */
IF
	InParty(Myself)
	InMyArea(Player1) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!Exists("C#AJPROS")
	Global("C#AjantisFTVerwechslung","GLOBAL",1)
THEN 
	RESPONSE #100
		CreateCreatureObjectOffScreen("C#AJPROS",Myself,0,0,0)
END

//----------------------------
//after slayer changes -> C#Ajan.baf









/* Open up globals - 2nd cycle. After leaving the underdark */

IF
  InParty(Myself)
  RealGlobalTimerExpired("C#AjantisFriendTalkT","GLOBAL")
  !AreaType(DUNGEON)
  !AreaCheck("AR1300")
  InMyArea(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !See([ENEMY])
  !Range([NEUTRAL],10)
  CombatCounter(0)
	!Global("C#AjantisNBThinking","GLOBAL",1)
  Global("C#AjantisSchweigen","GLOBAL",0) 
GlobalGT("Chapter","GLOBAL",%bg2_chapter_5%) 
    !Global("C#AjantisEngaged","GLOBAL",1)
    !Global("C#AjantisRomanceActive","GLOBAL",2)
OR(3)
    Global("C#AjantisFriendTalk","GLOBAL",15)
    Global("C#AjantisFriendTalk","GLOBAL",17)
    Global("C#AjantisFriendTalk","GLOBAL",19)
THEN
  RESPONSE #100
    IncrementGlobal("C#AjantisFriendTalk","GLOBAL",1)
END

/* triggering  - 2nd cycle. After leaving the underdark */

IF
  InParty(Myself)
  RealGlobalTimerExpired("C#AjantisFriendTalkT","GLOBAL")
  InMyArea(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !Range([NEUTRAL],10)
  Global("C#AjantisSchweigen","GLOBAL",0)
  !Global("C#AjantisEngaged","GLOBAL",1)
  !Global("C#AjantisRomanceActive","GLOBAL",2)
OR(3)
    Global("C#AjantisFriendTalk","GLOBAL",16)
    Global("C#AjantisFriendTalk","GLOBAL",18)
    Global("C#AjantisFriendTalk","GLOBAL",20)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#AjantisFriendTalkT","GLOBAL",%C#AJROM_TIMER%)
		PlaySong(0)
		PlaySound("C#ajanFT")
 StartDialogueNoSet(Player1)
END