DEFINE_ACTION_FUNCTION spawn
STR_VAR version=""
BEGIN
     
     LAF spawn_misc END
     OUTER_SPRINT general_spawns ~spwnbeh spwndrow spwngol spwnmind spwnmon spwnorg spwnrak spwntoa spwntrol spwnvamp spwnwolf tanhigh dw#spng~

     ACTION_MATCH ~%version%~ WITH
     1 BEGIN  // slight increase
         LAF spawn_xp INT_VAR one_million=400002 two_million=1000002 STR_VAR files=~%general_spawns%~  END
         LAF spawn_c6 STR_VAR spawn_chapter_level= EVAL ~OR(2)Global("Chapter","GLOBAL",%bg2_chapter_6%)XPGT(Player1,800000)~ END
     END
     2 BEGIN // significant increase
         LAF spawn_xp INT_VAR one_million=1 two_million=400002 STR_VAR files=~%general_spawns%~  END
         LAF spawn_c6 STR_VAR spawn_chapter_level= EVAL ~OR(2)Global("Chapter","GLOBAL",%bg2_chapter_6%)XPGT(Player1,400000)~ END
     END
     3 BEGIN  // almost-maximum increase
         LAF spawn_xp INT_VAR one_million=1 two_million=3 STR_VAR files=~%general_spawns%~  END
         LAF spawn_xp INT_VAR one_million=400002 two_million=1000002 STR_VAR files=spwndead  END
         LAF spawn_c6 STR_VAR spawn_chapter_level=~True()~ END
     END
     4 BEGIN
         LAF spawn_xp INT_VAR one_million=1 two_million=3 STR_VAR files=~%general_spawns% spwndead~  END
         LAF spawn_c6 STR_VAR spawn_chapter_level=~True()~ END
     END
     DEFAULT
       LAF warning STR_VAR warning=~Unrecognised spawn version~ END
     END



END

DEFINE_ACTION_FUNCTION spawn_misc BEGIN

     LAF swap_text STR_VAR files=ar3001.bcs swaps=~Global("SetDifficulty","AR3001",0)=>False()~ END
     LAF swap_text STR_VAR files=ar3007.bcs swaps=~LevelPartyGT(16)=>True()~ END
     LAF swap_text STR_VAR files=ar3012.bcs swaps=~Global("ReduceDemons","AR3012",0)=>False()~ END
     LAF swap_text STR_VAR files=ar3013.bcs swaps=~Global("ReduceDemons","AR3013",0)=>False()~ END
     LAF swap_text STR_VAR files=ar3016.bcs swaps=~Global("SetDifficulty","AR3016",0)=>False()~ END
     LAF swap_text STR_VAR files=ar4000.bcs swaps=~cut204f2=>cut204f cut204f3=>cut204f~ END
     LAF swap_text STR_VAR files=ar4500.bcs swaps=~SetGlobal("Chall1_Diff","AR4500",1)=>SetGlobal("Chall1_Diff","AR4500",4)
                                                   SetGlobal("Chall1_Diff","AR4500",2)=>SetGlobal("Chall1_Diff","AR4500",4)
                                                   SetGlobal("Chall1_Diff","AR4500",3)=>SetGlobal("Chall1_Diff","AR4500",4)~ END

END

DEFINE_ACTION_FUNCTION spawn_xp
    INT_VAR two_million=2000000
            one_million=1000000
    STR_VAR files=""
BEGIN

    OUTER_SET two_million_minus_one=two_million - 1
    OUTER_SET one_million_minus_one=one_million - 1
    LAF swap_text STR_VAR files postfix=bcs allow_missing=1
                                swaps=                ~2000000=>%two_million%
                                                       1999999=>%two_million_minus_one%
                                                       1000000=>%one_million%
                                                       999999=>%one_million_minus_one%
                                                       400000=>0
                                                       399999=>0
                                                       299999=>0~
    END

END

DEFINE_ACTION_FUNCTION spawn_c6
STR_VAR spawn_chapter_level=""
BEGIN
      LAF swap_text
          STR_VAR files=~c6behold c6goljug c6golsto c6mindul c6rakraj c6vamp c6vamver c6weregr~
                  postfix=bcs
                  swaps= EVAL ~Global("Chapter","GLOBAL",%bg2_chapter_6%)=>%spawn_chapter_level%~
      END
END
