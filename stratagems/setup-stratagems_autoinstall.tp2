BACKUP ~stratagems_external/autoinstall/backup~
AUTHOR ~davidw, Gibberlings3 forums~ // email address displayed if install fails

////////////////////////////////////////////////////////////////////
////	Language commands (direct from stratagems)
//////////////////////////////////////////////////////////////////////

ALWAYS
  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END
END

LANGUAGE
"English"
ENGLISH
 ~stratagems/tra/english/english.tra~

LANGUAGE
"Espanol (www.clandlan.net)"
SPANISH
 ~stratagems/tra/english/english.tra~
~stratagems/tra/spanish/spanish.tra~

LANGUAGE
"Polski (Yarpen)"
POLISH
 ~stratagems/tra/english/english.tra~
~stratagems/tra/polish/polish.tra~

LANGUAGE
"Deutsch (Leonardo Watson)"
GERMAN
 ~stratagems/tra/english/english.tra~
 ~stratagems/tra/german/german.tra~

LANGUAGE
"Francais (Mornagest/Isaya)"
FRENCH
 ~stratagems/tra/english/english.tra~
 ~stratagems/tra/french/french.tra~

 LANGUAGE
"Italiano (Stoneangel)"
ITALIAN
 ~stratagems/tra/english/english.tra~
 ~stratagems/tra/italian/italian.tra~

 LANGUAGE
"Russian (Prowler/Silent)"
RUSSIAN
 ~stratagems/tra/english/english.tra~
 ~stratagems/tra/russian/russian.tra~


 LANGUAGE
"Korean (Menocu)"
KOREAN
 ~stratagems/tra/english/english.tra~
 ~stratagems/tra/korean/korean.tra~


BEGIN ~stratagems quick install program~ DESIGNATED 100000 INSTALL_BY_DEFAULT NO_LOG_RECORD


   ACTION_IF !FILE_EXISTS ~stratagems_external/dir_marker.mrk~ BEGIN
      MKDIR ~stratagems_external~
      MKDIR ~stratagems_external/workspace~
      MKDIR ~stratagems_external/backup~
      <<<<<<<<.../stratagems-inline/dir_marker.mrk
      >>>>>>>>
      COPY ~.../stratagems-inline/dir_marker.mrk~ ~stratagems_external~
   END
   ACTION_IF "%WEIDU_OS%" STRING_EQUAL_CASE win32 BEGIN
      ACTION_IF !FILE_EXISTS setup-stratagems.exe BEGIN
         COPY weidu.exe setup-stratagems.exe
      END
   END

OUTER_SPRINT ~scsroot~ ~stratagems~
OUTER_SPRINT ~workspace~ ~stratagems_external/workspace~
INCLUDE ~%scsroot%/lib/install.tph~
LAF find_language RET language_num=language_num END
PRINT ~%LANGUAGE% %language_num%~

      <<<<<<<< autoinstall.txt
      >>>>>>>>
      COPY autoinstall.txt ~%workspace%~
      
ACTION_IF FILE_EXISTS ~%workspace%\dw#setup.mrk~ BEGIN
   PRINT ~Clearing up database; answer "yes" if asked to delete [anything].mrk!~
   AT_NOW ~del %workspace%\*.mrk~
END

ACTION_IF FILE_EXISTS ~override/dw#setup.mrk~ BEGIN
   PRINT ~Some components of Sword Coast Stratagems are already installed.
   
         Do you want to:
         1] Rebuild your installation from scratch?
         2] Fine-tune your install using the standard installer?
         3] Uninstall the entire mod?
         4] Quit this installer without changing anything?~
   OUTER_SET success=0
   OUTER_WHILE !success BEGIN
      ACTION_READLN choice
      ACTION_MATCH choice WITH 1 2 3 4 BEGIN
         OUTER_SET success=1
      END
      DEFAULT
         PRINT ~Please choose from 1, 2, 3 or 4~
      END
   END
   ACTION_MATCH choice WITH
   1 BEGIN
      AT_NOW ~setup-stratagems --no-exit-pause --uninstall~
      LAUNCH_ACTION_MACRO autoinstall_core
   END
   2 BEGIN
      AT_EXIT ~setup-stratagems --language %language_num%~
   END
   3 BEGIN
      AT_EXIT ~setup-stratagems --uninstall~
   END
   4 BEGIN
   END
   DEFAULT
   END
END ELSE BEGIN
   LAUNCH_ACTION_MACRO autoinstall_core
END








