INCLUDE ~%scsroot%/lib/ai_wrap.tph~
LAF include STR_VAR file=fiend_shared.tph locbase=fiend END

DEFINE_ACTION_FUNCTION demoncult BEGIN

    // set up Aec'letec

    LAF edit_creature STR_VAR creature=tanar tv=1 editstring=~nabassu=>null~ END
    LAF install STR_VAR file=~dw#demvt.spl dw#dmvta.spl dw#dmvtb.spl~ location=resource locbase=fiend END

    ACTION_IF GAME_IS ~bgt eet~ BEGIN
       OUTER_SPRINT tanari_gaze BGIN996
    END ELSE BEGIN
       OUTER_SPRINT ~tanari_gaze~ ~%TANARI_DEATH_GAZE%~
    END

    LAF ssl_to_bcs STR_VAR script=tanari location=~ssl~ rename_to=~%tutu_scriptbg%tanari%eet_var%~ END

    // resources
    
    <<<<<<<< .../stratagems-inline/dw#invis.baf
    IF
	OnCreation()
THEN
	RESPONSE #100
		ApplySpellRES("dw#silin",Myself)
END
   >>>>>>>>

   LAF install STR_VAR file=dw#invis.baf inline=yes END
   LAF clone_script STR_VAR script= ~dw#invis=>%tutu_scriptc%ultmage dw#invis=>%tutu_var%cultthf~ END

   // Tracea becomes 11th level
   
   MAKE_PATCH
      level=>11
      enforce_charclass=>is_bg1
      add_items=>dw#noinv
      immunity_to_spell=>~%TANARI_PARALYZE%~
   END
   LAF edit_creature STR_VAR creature=tracea tv=1 edits=patch_data END

   // enforcer becomes 10/10 level
   
   MAKE_PATCH
      level=>10
      level2=>10
      enforce_charclass=>~strict is_bg1~
   END
   LAF edit_creature STR_VAR creature=cultt1 tv=1 edits=patch_data END

   // give the Aec'letec cultists some combat ability

   MAKE_PATCH
      script_override=>~%tutu_scriptw%tasight~
      enforce_charclass=>~strict is_bg1~
      add_items=>dw#move0
      immunity_to_spell=>~%TANARI_PARALYZE%~
   END
   LAF edit_creature STR_VAR creature=~cultd1 cultd2 cultd3 cultd4 cultd5 cultd6~ tv=1 edits=patch_data END

   // cult wizards get better wands
   
   LAF edit_creature STR_VAR creature=cult2 tv=1 editstring=~swap_items=>"%tutu_var%wand08=>%tutu_var%wand04"~ END

END
