DEFINE_ACTION_FUNCTION find_language
RET language_num
BEGIN
ACTION_DEFINE_ASSOCIATIVE_ARRAY ~language_map~ BEGIN
   ~ENGLISH~ => 0
   ~SPANISH~ => 1
   ~POLISH~ => 2
   ~GERMAN~ => 3
   ~FRENCH~ => 4
   ~ITALIAN~ => 5
   ~RUSSIAN~ => 6
   ~KOREAN~ => 7
END

OUTER_SET ~language_num~ = $language_map(~%LANGUAGE%~)

END

DEFINE_ACTION_FUNCTION make_label
STR_VAR label=""
BEGIN 
 COPY_EXISTING misc01.itm ~%workspace%/%label%.mrk~
END

DEFINE_ACTION_FUNCTION run
STR_VAR version=""
BEGIN
   ACTION_IF !FILE_EXISTS ~%workspace%/autoinstall.txt~ BEGIN
      <<<<<<<< autoinstall.txt
      >>>>>>>>
      COPY autoinstall.txt ~%workspace%~
   END
   APPEND_OUTER ~%workspace%/autoinstall.txt~ ~ %COMPONENT_NUMBER%~
END

DEFINE_ACTION_FUNCTION make_weidu_string
RET weidustring
BEGIN
   LAF find_language RET language_num=language_num END
   OUTER_SPRINT weidustring ~setup-stratagems --uninstall --language %language_num%  --force-install-list 1000~
   ACTION_IF FILE_EXISTS ~%workspace%/do_not_install_from_scratch.mrk~ BEGIN
      OUTER_INNER_PATCH_SAVE ~weidustring~ ~%weidustring%~ BEGIN
         REPLACE_TEXTUALLY ~1000~ ~~
         REPLACE_TEXTUALLY ~--uninstall~ ~~
      END
   END
   COPY ~%workspace%/autoinstall.txt~ ~%workspace%~
        READ_2DA_ENTRIES_NOW auto_entries 1
        FOR (i=0;i<auto_entries;i+=1) BEGIN
           READ_2DA_ENTRY_FORMER auto_entries i 0 add_me
           SPRINT weidustring ~%weidustring% %add_me%~
        END
   BUT_ONLY
END


DEFINE_ACTION_MACRO autoinstall_core BEGIN

COPY ~%scsroot%/setup-stratagems.tp2~ ~%workspace%/auto.tp2~
      REPLACE_TEXTUALLY ~///---///~ ~~
      REPLACE_TEXTUALLY "REQUIRE_PREDICATE FILE_EXISTS ~override/dw#" "REQUIRE_PREDICATE FILE_EXISTS ~%workspace%/dw#"

OUTER_SET quit=0
ACTION_FOR_EACH component IN 2 3 4 5 6 16040 16041 BEGIN
   ACTION_IF quit=0 BEGIN
    OUTER_SET skip_this=0
    ACTION_IF component=16041 BEGIN
       ACTION_IF (GAME_IS ~tutu tutu_totsc bgee~) BEGIN
          OUTER_SET skip_this=1
       END
    END
    ACTION_IF component=16040 BEGIN
       ACTION_IF !(GAME_IS ~tutu tutu_totsc bgt eet bgee~) BEGIN
          OUTER_SET skip_this=1
       END
    END
    SILENT
    OUTER_SPRINT group_string ~GROUP @%component%~
   <<<<<<<< .../stratagems-inline/to_include
     OUTER_SPRINT group_name @xyz
   >>>>>>>>
    COPY ~.../stratagems-inline/to_include~ ~%workspace%~
      REPLACE_TEXTUALLY xyz ~%component%~
    REINCLUDE ~%workspace%/to_include~

    ACTION_IF skip_this=0 BEGIN
     PRINT ~~
     PRINT ~~
     PRINT ~//////////// %group_name% \\\\\\\\\\\\~
     PRINT ~What do you want to do with this group of components?~
     PRINT ~1: Install all of them (you will be asked about multiple-choice components)~
     PRINT ~2: Choose whether to install each component~
     PRINT ~3: Skip this group of components~
     PRINT ~Q: Quit without installing anything~
     OUTER_SET done=0
     OUTER_WHILE done=0 BEGIN
      ACTION_READLN choice
      ACTION_MATCH ~%choice%~ WITH
      1 BEGIN
           COPY ~%workspace%/auto.tp2~ ~%workspace%~
              REPLACE_TEXTUALLY ~%group_string% +SUBCOMPONENT~ ~NO_LOG_RECORD SUBCOMPONENT~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~NO_LOG_RECORD~
              REPLACE_TEXTUALLY ~%group_string%~ ~NO_LOG_RECORD INSTALL_BY_DEFAULT~
      OUTER_SET done=1
      END
      2 BEGIN
           COPY ~%workspace%/auto.tp2~ ~%workspace%~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~NO_LOG_RECORD~
              REPLACE_TEXTUALLY ~%group_string%~ ~NO_LOG_RECORD~
      OUTER_SET done=1
      END
      3 BEGIN
           COPY ~%workspace%/auto.tp2~ ~%workspace%~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~DEPRECATED ""~
              REPLACE_TEXTUALLY ~%group_string%~ ~DEPRECATED ""~
      OUTER_SET done=1
      END
      Q q BEGIN

      OUTER_SET quit=1
      OUTER_SET done=1
      END
      DEFAULT
      PRINT ~Please choose 1,2,3, or Q~
      END
    END // end of WHILE loop
   END ELSE BEGIN // end of skip_this loop
           COPY ~%workspace%/auto.tp2~ ~%workspace%~
              REPLACE_TEXTUALLY ~%group_string% +ASK_ANYWAY~ ~DEPRECATED ""~
              REPLACE_TEXTUALLY ~%group_string%~ ~DEPRECATED ""~

   END
  END // end of quit loop
END // end of ACTION loop

  ACTION_IF quit=0 BEGIN
     OUTER_SPRINT weidustring ~setup-stratagems --script-style bg2 --no-auto-tp2 --tlkout dialog.tlk --ftlkout dialogf.tlk --language %language_num% --autolog --no-exit-pause "%workspace%\auto.tp2"~
  
    AT_EXIT ~%weidustring%~

  END

END