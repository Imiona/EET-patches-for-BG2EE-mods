////////////Kit

IF
Global("MyKit","LOCALS",0)
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("MyKit","LOCALS",1)
AddKit("YxKit")
END

////////////Dialogues - I can't turn undead

IF
InParty(Myself)
!See([ENEMY])
CombatCounter(0)
PartyHasItem("YxTU")
Global("YvetteTurnUndead","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("YvetteTUTimer","GLOBAL",60)
SetGlobal("YvetteTurnUndead","GLOBAL",1)
END

IF
InParty(Myself)
CombatCounter(0)
Global("YvetteTurnUndead","GLOBAL",1)
RealGlobalTimerExpired("YvetteTUTimer","GLOBAL")
THEN
RESPONSE #100
StartDialogue("YxYveJ",Player1)
END

IF
InParty(Myself)
Global("YvetteTurnUndead","GLOBAL",2)
GlobalGT("CHAPTER","GLOBAL",%bg2_chapter_5%)
AreaCheck("AR0800")
THEN
RESPONSE #100
StartDialogue("YxYveJ",Player1)
END

IF
Global("YvetteTurnUndead","GLOBAL",3)
THEN
RESPONSE #100
SetGlobal("YvetteTurnUndead","GLOBAL",4)
DestroyItem("YxTU")
ApplySpellRES("YxTU2",Myself)
END

////////////Dialogues - romance

IF
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",5)
Global("YvetteRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("YvetteRomanceTalk","GLOBAL",6)
RealSetGlobalTimer("YvetteRomanceTime","GLOBAL",1800)
END

IF
InParty(Myself)
!AreaType(DUNGEON)
!See([ENEMY])
CombatCounter(0)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
RealGlobalTimerExpired("YvetteRomanceTime","GLOBAL")
OR(2)
Global("YvetteRomanceActive","GLOBAL",2)
Global("YvetteRomanceActive","GLOBAL",1)
OR(18)
Global("YvetteRomanceTalk","GLOBAL",6)
Global("YvetteRomanceTalk","GLOBAL",8)
Global("YvetteRomanceTalk","GLOBAL",10)
Global("YvetteRomanceTalk","GLOBAL",12)
Global("YvetteRomanceTalk","GLOBAL",14)
Global("YvetteRomanceTalk","GLOBAL",16)
Global("YvetteRomanceTalk","GLOBAL",19)
Global("YvetteRomanceTalk","GLOBAL",21)
Global("YvetteRomanceTalk","GLOBAL",23)
Global("YvetteRomanceTalk","GLOBAL",25)
Global("YvetteRomanceTalk","GLOBAL",27)
Global("YvetteRomanceTalk","GLOBAL",29)
Global("YvetteRomanceTalk","GLOBAL",33)
Global("YvetteRomanceTalk","GLOBAL",35)
Global("YvetteRomanceTalk","GLOBAL",40)
Global("YvetteRomanceTalk","GLOBAL",42)
Global("YvetteRomanceTalk","GLOBAL",44)
Global("YvetteRomanceTalk","GLOBAL",46)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
!AreaCheck("ARY001")
InParty(Myself)
CombatCounter(0)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
RealGlobalTimerExpired("YvetteRomanceTime","GLOBAL")
OR(2)
Global("YvetteRomanceActive","GLOBAL",2)
Global("YvetteRomanceActive","GLOBAL",1)
OR(16)
Global("YvetteRomanceTalk","GLOBAL",7)
Global("YvetteRomanceTalk","GLOBAL",9)
Global("YvetteRomanceTalk","GLOBAL",11)
Global("YvetteRomanceTalk","GLOBAL",13)
Global("YvetteRomanceTalk","GLOBAL",15)
Global("YvetteRomanceTalk","GLOBAL",18)
Global("YvetteRomanceTalk","GLOBAL",20)
Global("YvetteRomanceTalk","GLOBAL",24)
Global("YvetteRomanceTalk","GLOBAL",26)
Global("YvetteRomanceTalk","GLOBAL",28)
Global("YvetteRomanceTalk","GLOBAL",32)
Global("YvetteRomanceTalk","GLOBAL",34)
Global("YvetteRomanceTalk","GLOBAL",39)
Global("YvetteRomanceTalk","GLOBAL",41)
Global("YvetteRomanceTalk","GLOBAL",43)
Global("YvetteRomanceTalk","GLOBAL",45)
THEN
RESPONSE #100
IncrementGlobal("YvetteRomanceTalk","GLOBAL",1)
RealSetGlobalTimer("YvetteRomanceTime","GLOBAL",1800)
END

IF
InParty(Myself)
Global("YvetteRomanceActive","GLOBAL",1)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
Global("YLie","LOCALS",1)
THEN
RESPONSE #100
SetGlobal("YLie","LOCALS",2)
RealSetGlobalTimer("YvetteLie","GLOBAL",600)
END

IF
InParty(Myself)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
Global("YvetteRomanceActive","GLOBAL",1)
Global("YLie","LOCALS",2)
RealGlobalTimerExpired("YvetteLie","GLOBAL")
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
Global("YvetteRomanceTalk","GLOBAL",17)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
Global("YStraw","GLOBAL",1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
Global("YvetteEmily","GLOBAL",2)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
InParty(Myself)
CombatCounter(0)
RealGlobalTimerExpired("YvetteRomanceTime","GLOBAL")
Global("YvetteRomanceTalk","GLOBAL",22)
OR(2)
Global("YvetteRomanceActive","GLOBAL",2)
Global("YvetteRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
RealSetGlobalTimer("YvetteRomanceTime","GLOBAL",1800)
IncrementGlobal("YvetteRomanceTalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",30)
Global("YvetteRomanceActive","GLOBAL",1)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
THEN
RESPONSE #100
SetGlobal("YvetteRomanceTalk","GLOBAL",31)
SetGlobalTimer("YvetteSleepTimer","GLOBAL",1200)
END

IF
InParty(Myself)
PartyRested()
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteRomanceTalk","GLOBAL",31)
GlobalTimerExpired("YvetteSleepTimer","GLOBAL")
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",36)
Global("YvetteRomanceActive","GLOBAL",1)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
AreaCheck("AR0020")
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
AreaCheck("AR0020")
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
!Kit(Player2,YxKit)
THEN
RESPONSE #100
ActionOverride(Player2,LeaveAreaLUA("AR0021","",[623.164],0))
Continue()
END

IF
AreaCheck("AR0020")
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
!Kit(Player3,YxKit)
THEN
RESPONSE #100
ActionOverride(Player3,LeaveAreaLUA("AR0021","",[657.186],0))
Continue()
END

IF
AreaCheck("AR0020")
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
!Kit(Player3,YxKit)
THEN
RESPONSE #100
ActionOverride(Player4,LeaveAreaLUA("AR0021","",[583.213],0))
Continue()
END

IF
AreaCheck("AR0020")
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
!Kit(Player5,YxKit)
THEN
RESPONSE #100
ActionOverride(Player5,LeaveAreaLUA("AR0021","",[666.232],0))
Continue()
END

IF
AreaCheck("AR0020")
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
!Kit(Player6,YxKit)
THEN
RESPONSE #100
ActionOverride(Player6,LeaveAreaLUA("AR0021","",[590.220],0))
Continue()
END

IF
InParty(Myself)
AreaCheck("AR0020")
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
ActionOverride(Player1,LeaveAreaLUA("ARY001","",[387.477],0))
ActionOverride("YxYve",LeaveAreaLUA("ARY001","",[444.470],0))
END

IF
InParty(Myself)
Global("YvetteRomanceTalk","GLOBAL",38)
Global("YvetteRomanceActive","GLOBAL",1)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
AreaCheck("ARY001")
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

//Brynnlaw

IF
InParty(Myself)
!AreaType(DUNGEON)
!See([ENEMY])
CombatCounter(0)
AreaCheck("AR1600")
Global("YvetteBrynnlaw","GLOBAL",0)
OR(2)
Global("YvetteRomanceActive","GLOBAL",2)
Global("YvetteRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("YvetteBrynnlaw","GLOBAL",1)
RealSetGlobalTimer("YvetteBrynnlawTime","GLOBAL",360)
END

IF
InParty(Myself)
!AreaType(DUNGEON)
!See([ENEMY])
CombatCounter(0)
AreaCheck("AR1600")
RealGlobalTimerExpired("YvetteBrynnlawTime","GLOBAL")
Global("YvetteBrynnlaw","GLOBAL",1)
OR(2)
Global("YvetteRomanceActive","GLOBAL",2)
Global("YvetteRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
Global("CHAPTER","GLOBAL",%bg2_chapter_4%)
GlobalLT("YvetteRomanceTalk","GLOBAL",40)
Global("YvetteRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("YvetteRomanceActive","GLOBAL",3)
END


//////////////Viconia Conflict

IF
InParty(Myself)
InParty("Viconia")
Global("YvetteViconia","GLOBAL",0)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
InParty("Viconia")
Global("YvetteViconia","GLOBAL",1)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////////Lathander

IF
InParty(Myself)
AreaCheck("AR0902")
CombatCounter(0)
!See([ENEMY])
Global("YvetteLathander","GLOBAL",0)
!Global("YvetteRomanceActive","GLOBAL",3)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
PartyHasItem("MISC4X")
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////////Talos

IF
InParty(Myself)
AreaCheck("AR0904")
CombatCounter(0)
Global("YvetteTalos","GLOBAL",0)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////////Reputation - Yvette leaves

IF
InParty(Myself)
ReputationLT(Player1,7)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////////Book of Sune

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
HasItem("YxBook",Myself)
Global("YvetteHasBook","GLOBAL",0)
GlobalLT("CHAPTER","GLOBAL",%bg2_chapter_4%)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////////Taner

IF
InParty(Myself)
AreaCheck("AR0502")
Global("YvetteTanner","GLOBAL",0)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////////Spellhold plot

IF
InParty(Myself)
AreaCheck("AR1512")
Global("YvetteSpellhold","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YvetteSpellhold","GLOBAL",1)
RealSetGlobalTimer("YvetteSpellholdTime","GLOBAL",8)
END

IF
InParty(Myself)
AreaCheck("AR1512")
CombatCounter(0)
Global("YvetteSpellhold","GLOBAL",1)
RealGlobalTimerExpired("YvetteSpellholdTime","GLOBAL")
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
Global("YvetteSpellhold","GLOBAL",2)
!AreaCheck("AR1512")
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

IF
AreaCheck("AR1512")
Global("GeniePlat12","GLOBAL",1)
RealGlobalTimerExpired("GeniePlate12Time","GLOBAL")
Global("YvetteSpellhold","GLOBAL",2)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END


IF
Global("YvetteSpellhold","GLOBAL",3)
THEN
RESPONSE #100
SetGlobal("YvetteSpellhold","GLOBAL",4)
ActionOverride(Player1,LeaveAreaLUA("ARY002","",[434.448],0))
ActionOverride(Player2,LeaveAreaLUA("ARY002","",[494.505],0))
ActionOverride(Player3,LeaveAreaLUA("ARY002","",[381.493],0))
ActionOverride(Player4,LeaveAreaLUA("ARY002","",[571.446],0))
ActionOverride(Player5,LeaveAreaLUA("ARY002","",[322.457],0))
ActionOverride(Player6,LeaveAreaLUA("ARY002","",[268.410],0))
END

IF
!InParty(Myself)
AreaCheck("AR1514")
Global("YvetteSpellhold","GLOBAL",6)
THEN
RESPONSE #100
SetGlobal("YvetteSpellhold","GLOBAL",7)
ActionOverride(Myself,Polymorph(SLEEPING_WOMAN_HUMAN))
END

IF
Global("YvetteSpellhold","GLOBAL",9)
THEN
RESPONSE #100
SetGlobal("YvetteSpellhold","GLOBAL",10)
ActionOverride(Player1,LeaveAreaLUA("ARY003","",[807.713],0))
ActionOverride(Player2,LeaveAreaLUA("ARY003","",[641.797],0))
ActionOverride(Player3,LeaveAreaLUA("ARY003","",[724.861],0))
ActionOverride(Player4,LeaveAreaLUA("ARY003","",[849.855],0))
ActionOverride(Player5,LeaveAreaLUA("ARY003","",[899.789],0))
ActionOverride(Player6,LeaveAreaLUA("ARY003","",[702.734],0))
END

IF
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteSpellhold","GLOBAL",8)
Global("YvetteSpellhold2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YvetteSpellhold2","GLOBAL",1)
ActionOverride(Myself,Polymorph(THIEF_FEMALE_HUMAN))
JoinParty() 
END

IF
InParty(Myself)
CombatCounter(0)
AreaCheck("AR1514")
Global("YvetteRomanceActive","GLOBAL",1)
Global("YveCB","GLOBAL",2)
THEN
RESPONSE #100
SetGlobalTimer("YvetteAfterGabrielTime","GLOBAL",1200)
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
PartyRested()
Global("YvetteRomanceActive","GLOBAL",1)
GlobalTimerExpired("YvetteAfterGabrielTime","GLOBAL")
Global("YvetteAfterGabriel","GLOBAL",0)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
Global("AsylumPlot","GLOBAL",43)
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteSlayerChange","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("YvetteSlayerTimer","GLOBAL",180)
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteSlayerChange","GLOBAL",1)
RealGlobalTimerExpired("YvetteSlayerTimer","GLOBAL")
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteJournal","GLOBAL",0)
PartyHasItem("YxJour")
AreaCheck("ARY003")
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

/////////////Underwater talk

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR2300")
!Global("YvetteRomanceActive","GLOBAL",4)
Global("YvetteUnderwater","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YvetteUnderwater","GLOBAL",1)
RealSetGlobalTimer("YvetteUnderwaterTimer","GLOBAL",600)
END

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR2300")
!Global("YvetteRomanceActive","GLOBAL",4)
Global("YvetteUnderwater","GLOBAL",1)
RealGlobalTimerExpired("YvetteUnderwaterTimer","GLOBAL")
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

///////////Underdark

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR2100")
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteUnderdark","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YvetteUnderdark","GLOBAL",1)
RealSetGlobalTimer("YvetteUnderdarkTimer","GLOBAL",300)
END

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR2100")
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteUnderdark","GLOBAL",1)
RealGlobalTimerExpired("YvetteUnderdarkTimer","GLOBAL")
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
Global("YvetteRomanceActive","GLOBAL",1)
Global("YvetteUnderdark","GLOBAL",2)
RealGlobalTimerExpired("YvetteUnderdarkTimer","GLOBAL")
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

/////////////Vampire

IF
InParty(Myself)
PartyHasItem("C6Lantho")
GlobalLT("YvetteRomanceActive","GLOBAL",4)
Global("YvetteVampire","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("YvetteVampire","GLOBAL",1)
CreateCreatureObjectOffset("YxVamp",Myself,[10.10])
ActionOverride("YxVamp",StartDialogueNoSet(Player1))
END

IF
Global("YvetteVampire","GLOBAL",2)
Global("YvetteRomanceActive","Global",1)
THEN
RESPONSE #100
SetGlobal("YvetteVampire","GLOBAL",3)
Kill(Myself)
END

IF
Global("YvetteVampire","GLOBAL",2)
Global("YvetteRomanceActive","Global",3)
THEN
RESPONSE #100
SetGlobal("YvetteVampire","GLOBAL",3)
LeaveParty()
Kill(Myself)
END

IF
!Dead("YxYve")
Global("YvetteVampire","GLOBAL",3)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

/////////////love

IF
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR2500")
PartyHasItem("C6Lantho")
Global("YvetteVampire","GLOBAL",4)
Global("YvetteRomanceActive","Global",1)
Global("YvetteMoment","GLOBAL",0)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END

IF
InParty(Myself)
CombatCounter(0)
PartyRested()
Global("YvetteMoment","GLOBAL",1)
Global("YvetteRomanceActive","Global",2)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("YxRom")
StartDialog("YxYveJ",Player1)
END


/////////////Drunk talk

IF
Global("YvetteDrunkTalk","GLOBAL",0)
InParty(Myself)
!Global("YvetteRomanceActive","GLOBAL",3)
CheckStatGT(Player1,50,INTOXICATION)
OR(11)
AreaCheck("AR0509")
AreaCheck("AR0021")
AreaCheck("AR0406")
AreaCheck("AR0313")
AreaCheck("AR0704")
AreaCheck("AR0709")
AreaCheck("AR0513")
AreaCheck("AR1602")
AreaCheck("AR1105")
AreaCheck("AR2010")
AreaCheck("AR2202")
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

//////////////Eyeless

IF
Global("YvetteCultTalk","GLOBAL",0)
InParty(Myself)
AreaCheck("AR0202")
!See([ENEMY])
CombatCounter(0)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END

//////////////Crossmod TotDG

IF
InParty(Myself)
GlobalGT("GreenWitchQuest","GLOBAL",0)
Global("YvetteTotDG","GLOBAL",0)
AreaCheck("ARLDV2")
CombatCounter(0)
THEN
RESPONSE #100
StartDialog("YxYveJ",Player1)
END